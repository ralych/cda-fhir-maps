map "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaChVacdImmunizationCertificateDocumentToBundle" = "CdaChVacdImmunizationCertificateDocumentToBundle"


// ImmunizationCertificate document, 2.16.756.5.30.1.1.10.1.16
// 2021-02-06 Roeland Luykx, copyright RALY GmbH, Apache License 
// CDA-CH-VACD:  https://art-decor.org/art-decor/decor-project--cdachvacd-
// FHIR CH-VACD: http://fhir.ch/ig/ch-vacd/index.html

uses "http://hl7.org/fhir/cda/StructureDefinition/ClinicalDocument" alias ClinicalDocument as source
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedAuthor" alias AssignedAuthor as source
uses "http://hl7.org/fhir/cda/StructureDefinition/AssignedEntity" alias AssignedEntity as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Author" alias Author as source
uses "http://hl7.org/fhir/cda/StructureDefinition/CustodianOrganization" alias CustodianOrganization as source
uses "http://hl7.org/fhir/cda/StructureDefinition/IVL_TS" alias IVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/EIVL_TS" alias EIVL_TS as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Observation" alias Observation as source
uses "http://hl7.org/fhir/cda/StructureDefinition/PatientRole" alias PatientRole as source
uses "http://hl7.org/fhir/cda/StructureDefinition/RecordTarget" alias RecordTarget as source
uses "http://hl7.org/fhir/cda/StructureDefinition/Section" alias Section as source
uses "http://hl7.org/fhir/cda/StructureDefinition/SubstanceAdministration" alias SubstanceAdministration as source
uses "http://hl7.org/fhir/cda/StructureDefinition/SXPR_TS" alias SXPR_TS as source

uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as target
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as produced
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as produced
uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as produced
uses "http://hl7.org/fhir/StructureDefinition/Organization" alias Organization as produced
uses "http://hl7.org/fhir/StructureDefinition/PractitionerRole" alias PractitionerRole as produced
uses "http://hl7.org/fhir/StructureDefinition/Immunization" alias Immunization as produced
uses "http://hl7.org/fhir/StructureDefinition/ImmunizationRecommendation" alias ImmunizationRecommendation as produced
uses "http://hl7.org/fhir/StructureDefinition/Observation" alias Observation as produced

imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaToFhirTypes"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaToBundle"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaChToBundle"
imports "http://fhir.ch/ig/cda-fhir-maps/StructureMap/CdaChVacdToBundle"

// _________________________ Document Level Template  _________________________ 

// source: https://art-decor.org/art-decor/decor-templates--cdachvacd-?section=templates&id=2.16.756.5.30.1.1.10.1.16
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-vacd-composition-immunization-certificate.html
group CdaChVacdImmunizationCertificateDocumentToBundle(source cda : ClinicalDocument, target bundle : Bundle) {
  cda ->  bundle.entry as e, 
             e.resource = create('Composition') as composition, 
             composition.id = uuid() as uuid,
             e.fullUrl = append('urn:uuid:',uuid),
             bundle.entry as e2, 
             e2.resource = create('Patient') as patient,patient share patient,
             patient.id = uuid() as uuid2,
             e2.fullUrl = append('urn:uuid:',uuid2)
             then ClinicalDocumentCdaChVacdImmunizationCertificateDocumentToBundle(cda, patient, composition, bundle) "ClinicalDocumentToBody";
}

// source: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.1.16
// target: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationcard.html
group ClinicalDocumentCdaChVacdImmunizationCertificateDocumentToBundle(source cda : ClinicalDocument, target patient : Patient, target composition : Composition, target bundle : Bundle) extends ClinicalDocumentChToBundle {
  cda.component as component then {
    component.structuredBody as body then {
      body.component as component then {
        component.section as srcSection where (templateId.where(root='2.16.756.5.30.1.1.10.3.47')) -> composition.section as tgtSection then ImmunizationCertificateSectionContentModule(srcSection, patient, tgtSection, bundle);
      } "component";
    } "body";
  } "component";
}

// _________________________ Section Level Templates _________________________ 

// source: http://build.fhir.org/ig/hl7ch/ch-emed/StructureDefinition-ch-emed-composition-medicationcard.html
// target: https://art-decor.org/art-decor/decor-templates--cdachemed-?section=templates&id=2.16.756.5.30.1.1.10.4.89
group ImmunizationCertificateSectionContentModule(source src : Section, source patient : Patient, target tgt, target bundle: Bundle) extends ClinicalDocumentSection {
  src.entry as cdaEntry where (substanceAdministration.templateId.where(root='2.16.756.5.30.1.1.10.4.89')) ->  bundle.entry as e,  e.resource = create('Immunization') as immunization,  
    immunization.id = uuid() as uuid,
    e.fullUrl = append('urn:uuid:',uuid), 
    tgt.entry = create('Reference') as reference, reference.type='Immunization', reference.reference = append('urn:uuid:',uuid) then {
    cdaEntry.substanceAdministration as substanceAdministration then ImmunizationEntry(src, substanceAdministration, patient, immunization);
  } "cdaEntry";
}